plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.0.15'
    id 'pmd'
    id 'checkstyle'
    id 'info.solidsoft.pitest' version '1.19.0-rc.3'
}

group = 'dev.toonformat'
version = providers.gradleProperty('version').getOrElse('1.0.5')
description = 'Token-Oriented Object Notation (TOON) - A compact, human-readable format for LLM contexts'

// Apply external Gradle scripts
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/verification.gradle'

java {
    // Only enforce toolchain in CI to ensure consistent Java 17 builds
    // Locally, use whatever JDK is installed
    if (System.getenv('CI') == 'true') {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    mavenLocal()
}

jacoco {
    toolVersion = "0.8.14"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

pitest {
    pitestVersion = '1.17.4'
    targetClasses = ['dev.toonformat.jtoon.*']
    targetTests = ['dev.toonformat.jtoon.*']
    outputFormats = ['XML', 'HTML']
    mutationThreshold = 70
    coverageThreshold = 70
}

spotbugs {
    toolVersion = '4.9.8'
    excludeFilter = file('spotbugs-exclude.xml')
    effort = com.github.spotbugs.snom.Effort.valueOf("MAX")
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf("LOW")
    reportsDir = layout.buildDirectory.dir('spotbugs')
}

tasks.spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("${spotbugs.reportsDir.get()}/spotbugs-main.html")
        }
        xml {
            required = true
            outputLocation = file("${spotbugs.reportsDir.get()}/spotbugs-main.xml")
        }
    }
}

tasks.spotbugsTest {
    reports {
        html {
            required = true
            outputLocation = file("${spotbugs.reportsDir.get()}/spotbugs-test.html")
        }
        xml {
            required = true
            outputLocation = file("${spotbugs.reportsDir.get()}/spotbugs-test.xml")
        }
    }
    ignoreFailures = true
}

pmd {
    toolVersion = '7.0.0'
    ruleSetFiles = files('pmd-rules.xml')
    ruleSets = [] // Disable default rulesets, use custom file only
    consoleOutput = true
    ignoreFailures = true
}

tasks.pmdMain {
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.pmdTest {
    ruleSetFiles = files('pmd-rules-test.xml')
    ignoreFailures = true
    reports {
        html.required = true
        xml.required = true
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('checkstyle.xml')
    showViolations = true
    ignoreFailures = true
}

tasks.checkstyleMain {
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.checkstyleTest {
    enabled = false
    ignoreFailures = true
}

dependencies {
    implementation 'tools.jackson.core:jackson-databind:3.0.4'
    implementation 'tools.jackson.module:jackson-module-blackbird:3.0.4'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.8'
    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.awaitility:awaitility:4.3.0'
    testImplementation 'org.pitest:pitest-junit5-plugin:1.2.3'
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

// Check if running in CI or coverage is explicitly requested
boolean isCi = System.getenv('CI') == 'true'
boolean withCoverage = project.hasProperty('withCoverage') || isCi

test {
    useJUnitPlatform()
    // Only run jacoco coverage in CI or when explicitly requested
    if (withCoverage) {
        finalizedBy jacocoTestReport
        jacoco {
            enabled = true
        }
    } else {
        // Completely disable jacoco for local development
        jacoco {
            enabled = false
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    // Only enforce coverage thresholds in CI
    onlyIf { isCi }
    violationRules {
        rule {
            enabled = true
            element = "BUNDLE"
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = "0.90".toBigDecimal()
            }

            limit {
                counter = "BRANCH"
                value = "COVEREDRATIO"
                minimum = "0.80".toBigDecimal()
            }
        }
    }
}

// Only add coverage verification to check task in CI
if (isCi) {
    check.dependsOn jacocoTestCoverageVerification
}
check.dependsOn jacocoTestReport
check.dependsOn spotbugsMain
check.dependsOn pmdMain
check.dependsOn checkstyleMain

tasks.register('generateJavadoc', Javadoc) {
    description = 'Generates Javadoc HTML documentation in the docs/javadoc folder'
    group = 'docs'
    
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = file('docs/javadoc')
    
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

tasks.register('specsValidation', Test) {
    description = 'Run the schema validation test'
    group = 'verification'

    include '**/ConformanceTest.class'
}

tasks.register('jmh', JavaExec) {
    group = 'verification'
    description = 'Run JMH benchmarks'
    classpath = configurations.testRuntimeClasspath + sourceSets.test.runtimeClasspath
    mainClass = findProperty('benchmarkClass') ?: 'dev.toonformat.jtoon.JToonBenchmark'
    workingDir = projectDir
    doFirst {
        file('build/jmh-results').mkdirs()
    }
}

